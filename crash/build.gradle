import static groovy.io.FileType.FILES

apply plugin: 'java'
version = '1.0'

def services = "${buildDir}/plugins/META-INF/services/org.crsh.plugin.CRaSHPlugin"

// this is require because serviceConcat resolves artifacts defined in crash-sshd
project.evaluationDependsOn(':crash-sshd')

dependencies {
  compile group: 'org.crsh' , name:'crsh.cli', version:'1.2.7'
  compile group: 'org.crsh' , name:'crsh.shell.telnet', version:'1.2.7'
  compile group: 'jline' , name:'jline', version:'2.10'
  compile project(':cloudius')
  compile project(':crash-sshd')
}


task serviceConcat(type:Copy){
  def i = 0
  configurations.compile.resolvedConfiguration.resolvedArtifacts.collect{ zipTree(it.file)  }.each {d->
     from (d){
      rename 'org.crsh.plugin.CRaSHPlugin', "org.crsh.plugin.CRaSHPlugin-${i++}"
	include 'META-INF/services/**'
    }
  }
  into 'build/plugins'
  doLast {
    def res = new File(services)
    file('build/plugins/META-INF/services/').eachFileMatch(FILES,~/.*CRaSHPlugin\-\d/) {
     res << it.text
    }
  }
  outputs.file file(services)
}


jar {
    dependsOn configurations.runtime, serviceConcat
    from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
      exclude('META-INF/services/org.crsh.plugin.CRaSHPlugin')
      exclude('crash/crash.properties')
    }
    from(services){
    	into 'META-INF/services/'
    }
    exclude('org/bouncycastle/')
    exclude('META-INF/BCKEY.*') 
    exclude('crash/commands/base/login.groovy')
    exclude('crash/commands/base/java.groovy')
    manifest.attributes('Main-Class': 'com.cloudius.cli.Main')
}
