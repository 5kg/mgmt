apply plugin: 'java'

repositories {
  mavenCentral()
}

configurations {
    jruby
}

dependencies {
    jruby 'org.jruby:jruby-complete:1.7.4'
}

ext {
    gemsDir = file('vendor/gems')
    bundleDir = file('vendor/bundle/jruby/1.9/')
}

task installBundler(type: JavaExec) {
    outputs.dir gemsDir
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S gem install --no-rdoc --no-ri bundler -i ${gemsDir}".tokenize()

    doFirst {
        gemsDir.mkdirs()
    }
}

task bundleInstall(type: JavaExec, dependsOn:'installBundler') {
    environment GEM_HOME: gemsDir, GEM_PATH: gemsDir
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S bundle install --path=vendor/bundle --binstubs".tokenize()
}

task runapp(type: JavaExec){
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S bin/test".tokenize()
}

task puck(type: JavaExec){
    environment GEM_HOME: gemsDir, GEM_PATH: gemsDir
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S bin/puck --extra-files views/**".tokenize()

}

task gemList(type: JavaExec){
    environment GEM_HOME: bundleDir, GEM_PATH: bundleDir
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S gem list".tokenize()
}
